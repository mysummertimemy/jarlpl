ADD_STATE_TRIGGER ZORL 0 ~AreaCheck("%SWBaldursGate_MerchantLeague_L2%")~
ADD_STATE_TRIGGER ZORL 14 ~AreaCheck("%SWBaldursGate_MerchantLeague_L2%") GlobalLT("JA#MERCLEAGUE_INSP","GLOBAL",2)~
ADD_STATE_TRIGGER ZORL 5 ~AreaCheck("%SWBaldursGate_MerchantLeague_L2%") GlobalGT("JA#MERCLEAGUE_INSP","GLOBAL",1)~


REPLACE_TRANS_ACTION ZORL
BEGIN 5 END
BEGIN 1 END
~EscapeArea\(Destroy\)?([0-9]*)~
~NoAction()~


ALTER_TRANS ZORL
BEGIN 2 END
BEGIN 1 END
BEGIN
  "EPILOGUE" ~GOTO JA#ZORL_01~
END

ALTER_TRANS ZORL
BEGIN 3 END
BEGIN END
BEGIN
  "UNSOLVED_JOURNAL" ~@1028~
END

ALTER_TRANS ZORL
BEGIN 14 END
BEGIN 0 END
BEGIN
  "REPLY" ~@9~
  "EPILOGUE" ~EXIT~
END


EXTEND_BOTTOM ZORL 14
  IF ~Global("JA#ZORL_ASK","LOCALS",0)~ THEN REPLY @4 GOTO JA#ZORL_02
  IF ~Global("JA#SENDZORL","GLOBAL",1)~ THEN REPLY @3 GOTO JA#ZORL_03
END

EXTEND_BOTTOM ZORL 5
  IF ~Global("JA#SENDZORL","GLOBAL",1)~ THEN REPLY @3 GOTO JA#ZORL_03
END


APPEND ZORL

IF ~~ THEN JA#ZORL_01
  SAY @5
  IF ~~ THEN EXIT
END

IF ~~ THEN JA#ZORL_02
  SAY @6
  IF ~~ THEN DO ~SetGlobal("JA#ZORL_ASK","LOCALS",1)~ EXIT
END

IF ~~ THEN JA#ZORL_03
  SAY @7
  IF ~~ THEN DO ~SetGlobal("JA#SENDZORL","GLOBAL",2) EscapeArea()~ EXIT
END

END // APPEND ZORL
