REPLACE_ACTION_TEXT TENYA2
~EscapeArea\(Destroy\)?([0-9]*)~
~NoAction()~


REPLACE_STATE_TRIGGER TENYA2 6 ~GlobalLT("BOYBODY","GLOBAL",1)~

REPLACE_STATE_TRIGGER TENYA2 11 ~Global("JA#TENYA_QUEST","GLOBAL",3)~

EXTEND_BOTTOM TENYA2 11
  IF ~GlobalLT("BOYBODY","GLOBAL",1)~ THEN GOTO 0
END

ALTER_TRANS TENYA2
BEGIN 11 END
BEGIN END
BEGIN
  "JOURNAL" ~#%tenya_state28_journal%~
END


// BGT
ADD_STATE_TRIGGER TENYA2 0 ~GlobalLT("BOYBODY","GLOBAL",1)~
UNLESS ~GlobalLT("BOYBODY","GLOBAL",1)~

ADD_TRANS_TRIGGER TENYA2 0
~Global("HelpTremain","GLOBAL",1)~ DO 0 1 2 3 4
ADD_TRANS_TRIGGER TENYA2 6
~Global("HelpTremain","GLOBAL",1)~ DO 0 1 2 3


ADD_TRANS_ACTION TENYA2
BEGIN 3 5 7 10 END
BEGIN 0 END
~SetGlobal("JA#UMBERL_HOST","%BaldursGateDocks_WaterQueensHouse%",1)~


// BGT (like in BEE/EET)
ALTER_TRANS TENYA2
BEGIN 0 END
BEGIN 2 END
BEGIN
  "EPILOGUE" ~GOTO 4~
END

// BGT (like in BEE/EET)
ALTER_TRANS TENYA2
BEGIN 0 END
BEGIN 3 END
BEGIN
  "EPILOGUE" ~GOTO 3~
END


EXTEND_BOTTOM TENYA2 0
  IF ~!Global("HelpTremain","GLOBAL",1)~ THEN REPLY #%tenya2_state00_response2% GOTO JA#TENYA_08
  IF ~~ THEN REPLY @0 EXIT
END

EXTEND_BOTTOM TENYA2 6
  IF ~~ THEN REPLY @1 GOTO JA#TENYA_X0
END


APPEND TENYA2

IF ~~ THEN JA#TENYA_X0
  SAY @2
  COPY_TRANS_LATE TENYA2 10
END

IF WEIGHT #-2 ~Global("JA#TENYA_QUEST","GLOBAL",1)~ THEN JA#TENYA_00
  SAY @3 = @4 = @5
  IF ~~ THEN REPLY @6 GOTO JA#TENYA_02
  IF ~~ THEN REPLY @7 GOTO JA#TENYA_03
  IF ~~ THEN REPLY @8 GOTO JA#TENYA_01
END

IF ~~ THEN JA#TENYA_01
  SAY @9
  IF ~~ THEN DO ~EscapeArea()~ EXIT
END

IF ~~ THEN JA#TENYA_02
  SAY @10 = @11
  IF ~~ THEN DO ~SetGlobal("JA#TENYA_QUEST","GLOBAL",2)~ UNSOLVED_JOURNAL @1022 EXIT
END

IF ~~ THEN JA#TENYA_03
  SAY @12
  IF ~~ THEN REPLY @13 GOTO JA#TENYA_02
  IF ~~ THEN REPLY @14 GOTO JA#TENYA_04
END

IF ~~ THEN JA#TENYA_04
  SAY @15
  IF ~~ THEN DO ~EscapeArea()~ EXIT
END

IF WEIGHT #-1 ~Global("JA#TENYA_QUEST","GLOBAL",2)~ THEN JA#TENYA_05
  SAY @16
  IF ~!Dead("JA#TLOS1")~ THEN REPLY @17 GOTO JA#TENYA_09
  IF ~Dead("JA#TLOS1")~ THEN REPLY @18 GOTO JA#TENYA_07
  IF ~Dead("JA#TLOS1")~ THEN REPLY @19 GOTO JA#TENYA_07
  IF ~~ THEN REPLY @20 EXIT
END

IF ~~ THEN JA#TENYA_07
  SAY @21
  IF ~~ THEN DO ~EraseJournalEntry(@1022) SetGlobal("JA#TENYA_QUEST","GLOBAL",3) AddExperienceParty(1000) GiveItemCreate("JA#TENYA",LastTalkedToBy,1,0,0)~ SOLVED_JOURNAL @22 EXIT
END

IF ~~ THEN JA#TENYA_08
  SAY @23
  IF ~~ THEN EXIT
END

IF ~~ THEN JA#TENYA_09
  SAY @24
  IF ~~ THEN DO ~EraseJournalEntry(@1022) SetGlobal("JA#TENYA_QUEST","GLOBAL",10) SetGlobal("JA#UMBERL_HOST","%BaldursGateDocks_WaterQueensHouse%",1) Enemy()~ EXIT
END

END // APPEND TENYA2
