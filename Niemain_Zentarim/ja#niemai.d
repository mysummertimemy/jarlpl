REPLACE_SAY NIEMAI 2 @1


EXTEND_BOTTOM NIEMAI 3
  IF ~~ THEN GOTO JA#NIEMAI_3
END

EXTEND_BOTTOM NIEMAI 4
  IF ~~ THEN GOTO JA#NIEMAI_4
END


REPLACE_TRANS_ACTION NIEMAI
BEGIN 13 END
BEGIN END
~ActionOverride("WHEBER",EscapeArea\(Destroy\)?([0-9]*))[%WNL%%MNL%%LNL%%TAB% ]*ActionOverride("OULAM",EscapeArea\(Destroy\)?([0-9]*))[%WNL%%MNL%%LNL%%TAB% ]*ActionOverride("WILLIAM",EscapeArea\(Destroy\)?([0-9]*))[%WNL%%MNL%%LNL%%TAB% ]*EscapeArea\(Destroy\)?([0-9]*)~
~SetGlobal("JA#TalkedNiemai","GLOBAL",1)~


EXTEND_BOTTOM NIEMAI 9
  IF ~~ THEN REPLY @0 DO ~SetGlobal("JA#TalkedNiemai","GLOBAL",1)~ EXIT
END


APPEND NIEMAI

IF ~~ THEN JA#NIEMAI_3
  SAY @3
  COPY_TRANS NIEMAI 3
END

IF ~~ THEN JA#NIEMAI_4
  SAY @4
  COPY_TRANS NIEMAI 4
END


IF WEIGHT #-1 ~Global("JA#TalkedNiemai","GLOBAL",1)~ THEN JA#NIEMAI_6
  SAY @7
  IF ~~ THEN REPLY @8 DO ~SetGlobal("JA#TalkedNiemai","GLOBAL",2)~ GOTO JA#NIEMAI_7
  IF ~~ THEN REPLY @9 GOTO JA#NIEMAI_8
  IF ~~ THEN REPLY @10 EXIT
END

IF ~~ THEN JA#NIEMAI_7
  SAY @11
  IF ~~ THEN REPLY @12 GOTO JA#NIEMAI_9
  IF ~~ THEN REPLY @13 GOTO JA#NIEMAI_10
  IF ~~ THEN REPLY @14 EXIT
END

IF ~~ THEN JA#NIEMAI_8
  SAY @15
  COPY_TRANS NIEMAI 14 // Turn Niemain and his group hostile
END

IF ~~ THEN JA#NIEMAI_9
  SAY @16
  IF ~~ THEN EXIT
END

IF ~~ THEN JA#NIEMAI_10
  SAY @17
  IF ~~ THEN EXIT
END


IF WEIGHT #-1 ~Global("JA#TalkedNiemai","GLOBAL",2)~ THEN JA#NIEMAI_11
  SAY @18
  IF ~~ THEN EXIT
END

END // APPEND NIEMAI


CHAIN IF WEIGHT #-1 ~!InParty("XZAR") InParty("JAHEIRA") InMyArea("JAHEIRA") !StateCheck("JAHEIRA",CD_STATE_NOTVALID)~ THEN NIEMAI JA#NIEMAI_12
  @5 = @6
  == %JAHEIRA_JOINED% @32
  == NIEMAI @19
END
  COPY_TRANS NIEMAI 14 // Turn Niemain and his group hostile


ADD_TRANS_ACTION NIEMAI
BEGIN 2 JA#NIEMAI_3 JA#NIEMAI_4 END
BEGIN END
~SetGlobal("JA#NIEMAIN_SPWN","GLOBAL",1)~

ALTER_TRANS NIEMAI
BEGIN 2 JA#NIEMAI_3 JA#NIEMAI_4 END
BEGIN END
BEGIN
  "UNSOLVED_JOURNAL" ~@1024~
END



BEGIN ~JA#NIEMA~

IF ~Global("Chapter","GLOBAL",%tutu_chapter_7%)~ THEN JA#NIEMAI_x19
  SAY @48
  IF ~~ THEN REPLY @49 GOTO JA#NIEMAI_x20
  IF ~~ THEN REPLY @50 GOTO JA#NIEMAI_x21
END

IF ~~ THEN JA#NIEMAI_x20
  SAY @51
  IF ~~ THEN GOTO JA#NIEMAI_x21
END

IF ~~ THEN JA#NIEMAI_x21
  SAY @52
  IF ~~ THEN DO ~EscapeArea()~ JOURNAL @53 EXIT
END


IF ~Dead("XZAR")~ THEN JA#NIEMAI_13
  SAY @20 = @21
  IF ~~ THEN DO ~EraseJournalEntry(@1024) EscapeArea()~ EXIT
END


IF ~!InParty("XZAR")~ THEN JA#NIEMAI_xx0
  SAY @47
  IF ~~ THEN DO ~EraseJournalEntry(@1024) EscapeArea()~ EXIT
END

// END JA#NIEMA


CHAIN IF ~InParty("JAHEIRA") InMyArea("JAHEIRA") !StateCheck("JAHEIRA",CD_STATE_NOTVALID) Global("JA#7706JAHNIEMAIN","LOCALS",0)~ THEN JA#NIEMA JA#NIEMAI_20
  @22 = @23 DO ~SetGlobal("JA#7706JAHNIEMAIN","LOCALS",1)~
  == %JAHEIRA_JOINED% @33 = @34
  == JA#NIEMA @27
  == %JAHEIRA_JOINED% @35
  == JA#NIEMA @28
END
  IF ~Global("SearchIronThrone","GLOBAL",1)~ THEN REPLY @25 GOTO JA#NIEMAI_18
  IF ~~ THEN REPLY @26 EXIT


APPEND JA#NIEMA

IF ~True()~ THEN JA#NIEMAI_15
  SAY @24
  IF ~Global("SearchIronThrone","GLOBAL",1)~ THEN REPLY @25 GOTO JA#NIEMAI_18
  IF ~~ THEN REPLY @26 EXIT
END

IF ~~ THEN JA#NIEMAI_18
  SAY @29 = @30
  IF ~~ THEN DO ~SetGlobal("JA#NIEMAIN_SPWN","GLOBAL",3) EraseJournalEntry(@1024) AddExperienceParty(400) EscapeArea()~ SOLVED_JOURNAL @31 EXIT
END

END // APPEND JA#NIEMA



INTERJECT_COPY_TRANS2 NIEMAI 0 JA#Xzar_Niemain1
  == %XZAR_JOINED% IF ~InParty("XZAR") InMyArea("XZAR") !StateCheck("XZAR",CD_STATE_NOTVALID)~ THEN @36
END

INTERJECT_COPY_TRANS2 NIEMAI 5 JA#Monti_Niemain1
  == %MONTARON_JOINED% IF ~InParty("MONTARON") InMyArea("MONTARON") !StateCheck("MONTARON",CD_STATE_NOTVALID) Dead("XZAR")~ THEN @37
  == NIEMAI @38
  == %MONTARON_JOINED% @39
  == NIEMAI @40
END

INTERJECT_COPY_TRANS2 NIEMAI 6 JA#Monti_Niemain2
  == %MONTARON_JOINED% IF ~InParty("MONTARON") InMyArea("MONTARON") !StateCheck("MONTARON",CD_STATE_NOTVALID)~ THEN @41
END



BEGIN ~JA#ZENT1~

IF ~Global("JA#TalkedNiemai","GLOBAL",1)~ THEN JA#ZENT1_5
  SAY @45
  IF ~~ THEN EXIT
END

IF ~Global("JA#TalkedNiemai","GLOBAL",2)~ THEN JA#ZENT1_6
  SAY @46
  IF ~~ THEN EXIT
END

IF ~!InParty("XZAR") InParty("JAHEIRA") InMyArea("JAHEIRA") !StateCheck("JAHEIRA",CD_STATE_NOTVALID)~ THEN JA#ZENT1_4
  SAY @44
  IF ~~ THEN EXTERN NIEMAI JA#NIEMAI_12
END

IF ~InParty("XZAR")~ THEN JA#ZENT1_1
  SAY @42
  IF ~~ THEN EXTERN NIEMAI 0
END

IF ~InParty("MONTARON")~ THEN JA#ZENT1_2
  SAY @42
  IF ~~ THEN EXTERN NIEMAI 5
END

IF ~True()~ THEN JA#ZENT1_3
  SAY @43
  IF ~~ THEN EXTERN NIEMAI 8
END

// END JA#ZENT1
