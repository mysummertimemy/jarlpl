BEGIN ~JA#ARKHS~

IF ~StateCheck(Myself,STATE_CHARMED)~ THEN JA#ARKHS_CHARM
  SAY @3
  IF ~HasItem("JA#ARKKY",Myself) Global("JA#KNOW_KRAMM","LOCALS",2)~ THEN REPLY @4 GOTO JA#ARKHS_CHARM_KEY
  IF ~~ THEN REPLY @5 EXIT
END

IF ~~ THEN JA#ARKHS_CHARM_KEY
  SAY @1
  IF ~~ THEN DO ~GiveItem("JA#ARKKY",LastTalkedToBy)~ EXIT
END


IF ~NumTimesTalkedTo(0) Global("SearchIronThrone","GLOBAL",0)~ THEN JA#ARKHS_1
  SAY @6
  IF ~~ THEN REPLY @7 GOTO JA#ARKHS_1b
  IF ~~ THEN REPLY @8 EXIT
END

IF ~Global("JA#KNOW_KRAMM","LOCALS",0) Global("SearchIronThrone","GLOBAL",0)~ THEN JA#ARKHS_1a
  SAY @9
  IF ~~ THEN REPLY @7 GOTO JA#ARKHS_1b
  IF ~~ THEN REPLY @10 EXIT
END


IF ~Global("JA#KNOW_KRAMM","LOCALS",0) Global("SearchIronThrone","GLOBAL",1)~ THEN JA#ARKHS_ALR
  SAY @11
  IF ~~ THEN EXIT
END


IF ~~ THEN JA#ARKHS_1b
  SAY @12
  IF ~~ THEN REPLY @13 DO ~SetGlobal("JA#KNOW_KRAMM","LOCALS",1)~ GOTO JA#ARKHS_1c
  IF ~~ THEN REPLY @14 EXIT
END

IF ~~ THEN JA#ARKHS_1c
  SAY @15
  IF ~Global("JA#QU_PROB","LOCALS",0)~ THEN REPLY @16 DO ~SetGlobal("JA#QU_PROB","LOCALS",1)~ GOTO JA#ARKHS_1d
  IF ~Global("JA#QU_BASI","LOCALS",0)~ THEN REPLY @17 DO ~SetGlobal("JA#QU_BASI","LOCALS",1)~ GOTO JA#ARKHS_1e
  IF ~~ THEN REPLY @18 GOTO JA#ARKHS_1f
  IF ~~ THEN REPLY @19 EXIT
END

IF ~~ THEN JA#ARKHS_1d
  SAY @20
  IF ~~ THEN REPLY @21 GOTO JA#ARKHS_1f
  IF ~~ THEN REPLY @22 EXIT
END

IF ~~ THEN JA#ARKHS_1e
  SAY @23 = @24
  IF ~~ THEN REPLY @25 GOTO JA#ARKHS_1g
  IF ~~ THEN REPLY @26 GOTO JA#ARKHS_1f
  IF ~~ THEN REPLY @22 EXIT
END

IF ~~ THEN JA#ARKHS_1f
  SAY @27
  IF ~Global("JA#QU_PROB","LOCALS",0)~ THEN REPLY @28 DO ~SetGlobal("JA#QU_PROB","LOCALS",1)~ GOTO JA#ARKHS_1l
  IF ~Global("JA#QU_BASI","LOCALS",0)~ THEN REPLY @29 DO ~SetGlobal("JA#QU_BASI","LOCALS",1)~ GOTO JA#ARKHS_1e
  IF ~Global("JA#HUSAM_SECRET","GLOBAL",1)~ THEN REPLY @30 GOTO JA#ARKHS_1i
  IF ~Global("JA#HUSAM_SECRET","GLOBAL",2)~ THEN REPLY @31 GOTO JA#ARKHS_2
  IF ~~ THEN REPLY @32 GOTO JA#ARKHS_1k
  IF ~~ THEN REPLY @22 EXIT
END

IF ~~ THEN JA#ARKHS_1g
  SAY @33
  IF ~~ THEN REPLY @21 GOTO JA#ARKHS_1f
  IF ~~ THEN REPLY @22 EXIT
END

IF ~Global("JA#KNOW_KRAMM","LOCALS",1) Global("SearchIronThrone","GLOBAL",0)~ THEN JA#ARKHS_1h
  SAY @34
  IF ~~ THEN REPLY @35 GOTO JA#ARKHS_1f
  IF ~~ THEN REPLY @36 EXIT
END

IF ~~ THEN JA#ARKHS_1i
  SAY @37
  IF ~~ THEN REPLY @38 GOTO JA#ARKHS_1j
END

IF ~~ THEN JA#ARKHS_1j
  SAY @39
  IF ~~ THEN REPLY @40 GOTO JA#ARKHS_3
  IF ~~ THEN REPLY @41 GOTO JA#ARKHS_3
  IF ~~ THEN REPLY @42 GOTO JA#ARKHS_4
  IF ~~ THEN REPLY @43 GOTO JA#ARKHS_2a
END

IF ~~ THEN JA#ARKHS_1k
  SAY @44 = @45 = @46
  IF ~~ THEN REPLY @21 GOTO JA#ARKHS_1f
  IF ~~ THEN REPLY @22 EXIT
END

IF ~~ THEN JA#ARKHS_1l
  SAY @98
  IF ~~ THEN REPLY @21 GOTO JA#ARKHS_1f
  IF ~~ THEN REPLY @22 EXIT
END


IF ~~ THEN JA#ARKHS_2
  SAY @47
  IF ~~ THEN REPLY @40 GOTO JA#ARKHS_3
  IF ~~ THEN REPLY @41 GOTO JA#ARKHS_3
  IF ~~ THEN REPLY @42 GOTO JA#ARKHS_4
  IF ~~ THEN REPLY @43 GOTO JA#ARKHS_2a
END

IF ~~ THEN JA#ARKHS_2a
  SAY @48
  IF ~~ THEN REPLY @40 GOTO JA#ARKHS_3
  IF ~~ THEN REPLY @41 GOTO JA#ARKHS_3
  IF ~~ THEN REPLY @42 GOTO JA#ARKHS_4
END

IF ~~ THEN JA#ARKHS_3
  SAY @49 = @50
  IF ~~ THEN REPLY @51 GOTO JA#ARKHS_5
  IF ~~ THEN REPLY @52 GOTO JA#ARKHS_6
END

IF ~~ THEN JA#ARKHS_4
  SAY @53
  IF ~~ THEN REPLY @54 GOTO JA#ARKHS_3
  IF ~~ THEN REPLY @41 GOTO JA#ARKHS_3
END

IF ~~ THEN JA#ARKHS_5
  SAY @55
  IF ~~ THEN REPLY @56 GOTO JA#ARKHS_6
  IF ~~ THEN REPLY @57 GOTO JA#ARKHS_11
END

IF ~~ THEN JA#ARKHS_6
  SAY @58
  IF ~~ THEN GOTO JA#ARKHS_6_b
END

IF ~~ THEN JA#ARKHS_6_b
  SAY @59
  IF ~~ THEN REPLY @60 GOTO JA#ARKHS_7
  IF ~~ THEN REPLY @61 GOTO JA#ARKHS_7
END

IF ~~ THEN JA#ARKHS_7
  SAY @62 = @63 = @64
  IF ~~ THEN REPLY @65 GOTO JA#ARKHS_8
  IF ~~ THEN REPLY @66 GOTO JA#ARKHS_9
  IF ~~ THEN REPLY @67 GOTO JA#ARKHS_10
  IF ~~ THEN REPLY @2 EXIT
END

IF ~~ THEN JA#ARKHS_8
  SAY @68
  IF ~~ THEN DO ~GiveItem("JA#ARKKY",LastTalkedToBy) SetGlobal("JA#KNOW_KRAMM","LOCALS",3)~ UNSOLVED_JOURNAL @1010 EXIT
END

IF ~~ THEN JA#ARKHS_9
  SAY @69
  IF ~~ THEN DO ~GiveItem("JA#ARKKY",LastTalkedToBy) SetGlobal("JA#KNOW_KRAMM","LOCALS",3)~ UNSOLVED_JOURNAL @1010 EXIT
END

IF ~~ THEN JA#ARKHS_10
  SAY @70
  IF ~~ THEN REPLY @71 GOTO JA#ARKHS_8
  IF ~~ THEN REPLY @72 DO ~Enemy()~ EXIT
  IF ~~ THEN REPLY @2 EXIT
END

IF ~~ THEN JA#ARKHS_11
  SAY @73
  IF ~~ THEN REPLY @74 DO ~Enemy()~ EXIT
  IF ~~ THEN REPLY @75 DO ~SetGlobal("JA#KNOW_KRAMM","LOCALS",2)~ EXIT
END

IF ~Global("SearchIronThrone","GLOBAL",0) GlobalGT("JA#KNOW_KRAMM","LOCALS",1)~ THEN JA#ARKHS_12
  SAY @76
  IF ~Global("JA#KNOW_KRAMM","LOCALS",2)~ THEN REPLY @97 GOTO JA#ARKHS_6_b
  IF ~HasItem("JA#ARKKY",Myself)~ THEN REPLY @77 GOTO JA#ARKHS_6
  IF ~~ THEN REPLY @78 EXIT
END

IF ~Dead("Thaldo") GlobalGT("SearchIronThrone","GLOBAL",0)~ THEN JA#ARKHS_13
  SAY @79 = @80
  IF ~~ THEN DO ~SetGlobal("JA#SAVETHALDO","GLOBAL",10) EraseJournalEntry(@1010) Enemy()~ SOLVED_JOURNAL @81 EXIT
END


IF ~Global("JA#SAVETHALDO","GLOBAL",2) GlobalGT("SearchIronThrone","GLOBAL",0)~ THEN JA#ARKHS_14
  SAY @82
  IF ~~ THEN DO ~SetGlobal("JA#SAVETHALDO","GLOBAL",3) GiveItemCreate("JA#ARKHS",Player1,1,0,0) EraseJournalEntry(@1010)~ SOLVED_JOURNAL @1019 EXTERN JA#THALD JA#THALD_00
END

IF ~~ THEN JA#ARKHS_15
  SAY @88
  IF ~~ THEN EXIT
END

IF ~~ THEN JA#ARKHS_16
  SAY @89
  IF ~~ THEN EXIT
END


IF ~RandomNum(3,1)~ THEN JA#ARKHS_18
  SAY @94
  IF ~~ THEN EXIT
END

IF ~RandomNum(3,2)~ THEN JA#ARKHS_19
  SAY @95
  IF ~~ THEN EXIT
END

IF ~RandomNum(3,3)~ THEN JA#ARKHS_20
  SAY @96
  IF ~~ THEN EXIT
END

// END ~JA#ARKHS~


CHAIN JA#THALD JA#THALD_00
  @83
  == JA#ARKHS @84
END
  IF ~~ THEN REPLY @85 EXTERN JA#ARKHS JA#ARKHS_15
  IF ~~ THEN REPLY @86 EXTERN JA#ARKHS JA#ARKHS_16
  IF ~~ THEN REPLY @87 EXTERN JA#ARKHS JA#ARKHS_17


CHAIN JA#ARKHS JA#ARKHS_17
  @90
  == JA#THALD @91
  == JA#ARKHS @92 = @93
EXIT
