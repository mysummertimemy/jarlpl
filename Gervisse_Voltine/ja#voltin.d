REPLACE_STATE_TRIGGER VOLTIN 0 ~AreaCheck("JA#C08")~
// REPLACE_STATE_TRIGGER VOLTIN 3 ~False()~ // State is linked in response of gervisse_is_dead


ADD_TRANS_ACTION VOLTIN
BEGIN 1 END
BEGIN END
~SetGlobal("JA#VOLT_QUEST","GLOBAL",1)~


ADD_TRANS_ACTION VOLTIN
BEGIN 4 END
BEGIN 0 END
~Enemy()~

/* Faldorn will not tolerate if Voltine is attacked. Taken from bg1npc Project */
INTERJECT_COPY_TRANS2 VOLTIN 4 JA#FalMadVoltine
  == %FALDORN_JOINED% IF ~InParty("faldorn") InMyArea("faldorn") !StateCheck("faldorn",CD_STATE_NOTVALID)~ THEN @90
  DO ~ActionOverride("voltine",Enemy()) LeaveParty() SetLeavePartyDialogFile() ChangeAIScript("",DEFAULT) Enemy()~
END


APPEND VOLTIN

IF WEIGHT #0 ~!Dead("Gervisse") Global("JA#VOLT_QUEST","GLOBAL",0)~ THEN JA_VOLTIN_1
  SAY @30
  IF ~Global("JA#GERVIS_QUEST","GLOBAL",1)~ THEN REPLY @31 GOTO JA_VOLTIN_2
  IF ~Global("JA#GERVIS_QUEST","GLOBAL",10)~ THEN REPLY @32 GOTO JA_VOLTIN_3
  IF ~~ THEN REPLY @33 GOTO 4
  IF ~~ THEN REPLY @91 GOTO 1
  IF ~~ THEN REPLY @92 GOTO JA_VOLTIN_5
END

IF ~~ THEN JA_VOLTIN_2
  SAY @35
  IF ~~ THEN REPLY @36 GOTO 4
  IF ~~ THEN REPLY @37 GOTO JA_VOLTIN_3
  IF ~~ THEN REPLY @34 GOTO JA_VOLTIN_5
END

IF ~~ THEN JA_VOLTIN_3
  SAY @38 = @39
  IF ~~ THEN REPLY @36 GOTO 4
  IF ~~ THEN REPLY @40 GOTO JA_VOLTIN_4
  IF ~~ THEN REPLY @41 GOTO 4
  IF ~~ THEN REPLY @34 GOTO JA_VOLTIN_5
END

IF ~~ THEN JA_VOLTIN_4
  SAY @42
  IF ~~ THEN DO ~SetGlobal("JA#VOLT_QUEST","GLOBAL",2)~ UNSOLVED_JOURNAL @1043 EXIT
END

IF ~~ THEN JA_VOLTIN_5
  SAY @44
  IF ~~ THEN EXIT
END


IF WEIGHT #0 ~!Dead("Gervisse") Global("JA#VOLT_QUEST","GLOBAL",1)~ THEN JA_VOLTIN_6
  SAY @46
  IF ~Global("JA#GERVIS_QUEST","GLOBAL",1)~ THEN REPLY @31 GOTO JA_VOLTIN_2
  IF ~Global("JA#GERVIS_QUEST","GLOBAL",10)~ THEN REPLY @32 GOTO JA_VOLTIN_3
  IF ~~ THEN REPLY @33 GOTO 4
  IF ~~ THEN REPLY @34 GOTO JA_VOLTIN_5
  IF ~~ THEN REPLY @48 GOTO JA_VOLTIN_8
END


IF WEIGHT #0 ~!Dead("Gervisse") Global("JA#VOLT_QUEST","GLOBAL",2)~ THEN JA_VOLTIN_7
  SAY @47
  IF ~~ THEN REPLY @49 GOTO 4
  IF ~~ THEN REPLY @34 GOTO JA_VOLTIN_5
  IF ~~ THEN REPLY @48 GOTO JA_VOLTIN_8
END

IF ~~ THEN JA_VOLTIN_8
  SAY @51
  IF ~~ THEN EXIT
END


IF WEIGHT #0 ~Dead("Gervisse") Global("HelpVoltine","GLOBAL",0)~ THEN gervisse_is_dead
  SAY @88
  IF ~~ THEN REPLY @89 GOTO 3
  IF ~~ THEN REPLY @29 GOTO 4
END

END // APPEND VOLTIN
