REPLACE_STATE_TRIGGER GERVIS 0 ~NumTimesTalkedTo(0)~
// REPLACE_STATE_TRIGGER GERVIS 1 ~False()~ // Strref is linked in response of JA_GERVIS_0
ADD_STATE_TRIGGER GERVIS 2 ~Global("JA#GERVIS_QUEST","GLOBAL",1)~ // State is linked in response of voltine_is_dead
ADD_STATE_TRIGGER GERVIS 3 ~Global("JA#GERVIS_QUEST","GLOBAL",1)~


REPLACE_TRANS_ACTION GERVIS
BEGIN 2 END
BEGIN END
~EscapeArea\(Destroy\)?([0-9]*)~
~SetGlobal("JA#GERVIS_QUEST","GLOBAL",2)~


ADD_TRANS_ACTION GERVIS
BEGIN 4 END
BEGIN 0 END
~SetGlobal("JA#GERVIS_QUEST","GLOBAL",10) Enemy()~


ALTER_TRANS GERVIS
BEGIN 0 END
BEGIN 0 END
BEGIN
  "ACTION" ~SetGlobal("JA#KNOWGERVIS","%NWBaldursGate_LaertasHouse_L1%",1)~
  "EPILOGUE" ~GOTO JA_GERVIS_0~
END


ALTER_TRANS GERVIS
BEGIN 3 END
BEGIN 0 END
BEGIN
  "REPLY" ~@85~
END

EXTEND_BOTTOM GERVIS 3
  IF ~~ THEN REPLY @6 GOTO 4
  IF ~Global("JA#VOLT_QUEST","GLOBAL",2)~ THEN REPLY @7 GOTO 4
END


APPEND GERVIS

IF ~~ THEN JA_GERVIS_0
  SAY #%gervis_state01_strref%
  IF ~~ THEN REPLY @83 GOTO JA_GERVIS_5
  IF ~~ THEN REPLY @3 DO ~SetGlobal("JA#WHY","LOCALS",1)~ GOTO JA_GERVIS_1
  IF ~GlobalGT("JA#VOLT_QUEST","GLOBAL",0)~ THEN REPLY @4 DO ~SetGlobal("JA#OFFEND","LOCALS",1)~ GOTO JA_GERVIS_2
  IF ~~ THEN REPLY @5 GOTO JA_GERVIS_3
END

IF ~~ THEN JA_GERVIS_1
  SAY @8
  IF ~~ THEN REPLY @9 GOTO JA_GERVIS_5
  IF ~GlobalGT("JA#VOLT_QUEST","GLOBAL",0) Global("JA#OFFEND","LOCALS",0)~ THEN REPLY @4 DO ~SetGlobal("JA#OFFEND","LOCALS",1)~ GOTO JA_GERVIS_2
  IF ~Global("JA#OFFEND","LOCALS",1)~ THEN REPLY @10 GOTO 4
  IF ~~ THEN REPLY @11 GOTO JA_GERVIS_4
  IF ~~ THEN REPLY @5 GOTO JA_GERVIS_3
END

IF ~~ THEN JA_GERVIS_2
  SAY @12
  IF ~~ THEN REPLY @9 GOTO JA_GERVIS_5
  IF ~Global("JA#WHY","LOCALS",0)~ THEN REPLY @3 DO ~SetGlobal("JA#WHY","LOCALS",1)~ GOTO JA_GERVIS_1
  IF ~GlobalGT("JA#VOLT_QUEST","GLOBAL",0)~ THEN REPLY @10 GOTO 4
  IF ~~ THEN REPLY @11 GOTO JA_GERVIS_4
  IF ~~ THEN REPLY @5 GOTO JA_GERVIS_3
END

IF ~~ THEN JA_GERVIS_3
  SAY @13
  IF ~~ THEN DO ~SetGlobal("JA#GERVIS_QUEST","GLOBAL",10)~ EXIT
END

IF ~~ THEN JA_GERVIS_4
  SAY @14
  IF ~~ THEN DO ~SetGlobal("JA#GERVIS_QUEST","GLOBAL",10)~ EXIT
END

IF ~~ THEN JA_GERVIS_5
  SAY @15 = @16
  IF ~~ THEN DO ~SetGlobal("JA#GERVIS_QUEST","GLOBAL",1)~ UNSOLVED_JOURNAL #%gervis_state01_journal% EXIT
END


IF WEIGHT #%gervis_state01_weight% ~Global("JA#GERVIS_QUEST","GLOBAL",1) Dead("Voltine")~ THEN voltine_is_dead
  SAY @91
  IF ~~ THEN REPLY @92 GOTO 2
  IF ~~ THEN REPLY @6 GOTO 4
END


IF ~Global("JA#GERVIS_QUEST","GLOBAL",10)~ THEN JA_GERVIS_6
  SAY @17
  IF ~!Dead("Voltine") Global("JA#VOLT_QUEST","GLOBAL",2)~ THEN REPLY @18 GOTO 4
  IF ~ Dead("Voltine")~ THEN REPLY @19 GOTO 4
  IF ~!Dead("Voltine")~ THEN REPLY @20 GOTO JA_GERVIS_5
  IF ~~ THEN REPLY @21 EXIT
END


IF ~Global("JA#GERVIS_QUEST","GLOBAL",2)~ THEN JA_GERVIS_7
  SAY @22
  IF ~~ THEN REPLY @23 GOTO JA_GERVIS_9
  IF ~~ THEN REPLY @24 GOTO JA_GERVIS_8
  IF ~~ THEN REPLY @25 GOTO 4
END

IF ~~ THEN JA_GERVIS_8
  SAY @26
  IF ~~ THEN REPLY @27 GOTO JA_GERVIS_9
  IF ~~ THEN REPLY @25 GOTO 4
END

IF ~~ THEN JA_GERVIS_9
  SAY @28
  IF ~~ THEN EXIT
END

END // APPEND GERVIS
